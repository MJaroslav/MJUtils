def buildprop = new Properties()
file("build.properties").withInputStream { buildprop.load(it) }

subprojects {
    buildscript {
        repositories {
            mavenCentral()
        }
    }

    apply plugin: 'java'
    apply plugin: 'idea'

    configurations {
        shade

        compile.extendsFrom(shade)
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

    idea.module.inheritOutputDirs = true

    dependencies {
        compileOnly "org.projectlombok:lombok:${buildprop.lombok_version}"
        annotationProcessor "org.projectlombok:lombok:${buildprop.lombok_version}"

        testCompileOnly "org.projectlombok:lombok:${buildprop.lombok_version}"
        testAnnotationProcessor "org.projectlombok:lombok:${buildprop.lombok_version}"
    }

    tasks.withType(Jar)*.doFirst {
        includeEmptyDirs = false
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        if (name == "jar" || name == "devJar" || name == "sourcesJar")
            configurations.shade.each { dep ->
                from project.zipTree(dep)
            }
    }

    task devJar(type: Jar) {
        classifier = 'dev'
        from sourceSets.main.output
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        from "${projectDir.path}/build/docs/javadoc"
        classifier "javadoc"
    }

    artifacts {
        archives devJar
        archives sourcesJar
        archives javadocJar
    }
}

